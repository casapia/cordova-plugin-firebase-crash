buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.4'
        classpath 'com.google.gms:google-services:4.3.14'
        // plugin crash with > 2.9.1 (current 2.9.5)
        String gradlePluginFirebaseCrashlyticsClassPath = "com.google.firebase:firebase-crashlytics-gradle:2.9.1"
        println "Adding classpath: ${gradlePluginFirebaseCrashlyticsClassPath}"
        classpath gradlePluginFirebaseCrashlyticsClassPath
    }
}

println "Adding com.google.firebase.crashlytics plugin"
apply plugin: com.google.firebase.crashlytics.buildtools.gradle.CrashlyticsPlugin

android {
    buildTypes {
        debug {
            // do not upload debug versions to crashlytics
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
    }
}

// use postBuildExtras to make sure the plugin is applied after
// cdvPluginPostBuildExtras. Therefore if googleServices is added
// to cdvPluginPostBuildExtras somewhere else, the plugin execution
// will be skipped and project build will be successfull
ext.postBuildExtras = {
    if (project.extensions.findByName('googleServices') == null) {
        // apply plugin: 'com.google.gms.google-services'
        // class must be used instead of id(string) to be able to apply plugin from non-root gradle file
        apply plugin: com.google.gms.googleservices.GoogleServicesPlugin
        googleServices.disableVersionCheck = true
    }
}
